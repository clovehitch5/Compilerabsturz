G = ( N, Z, T, P )

N = {
  Z, Programm, Deklarationsteil, Deklaration, Trennlinie, Ausfuehrungsteil,
  AnweisungsBlock, Anweisung, Zuweisung, Inkrementierung, BedingteAnweisung,
  Schleife, ForSchleife, Typ, BezeichnerDeklaration, Bezeichner,
  StringAusdruck, StringAtom, Zeichenkette, MathematischerAusdruck,
  Summand, Faktor, Operand, GanzzahlLiteral, GleitkommaLiteral,
  BoolescherAusdruck, VergleichsOperator, Ziffer, Buchstabe, Grossbuchstabe,
  Leerzeichen, WSO
}

T = {
  ' ', 'A'...'Z', 'a'...'z', '0'...'9',
  ',', '.', '<=', '"', ':', '+', '-', '*', '/', '(', ')', '<', '>', '++', '--',
  '&&', '||', 'int', 'float', 'string', 'if', 'else', 'while', 'begin', 'end',
  'for', '...', 'program'
}

P = {

Z ::= Programm Deklarationsteil Trennlinie Ausfuehrungsteil .

Programm ::= 'program' WSO Bezeichner ':' .

Deklarationsteil ::= { Deklaration } .

Deklaration ::= Typ WSO BezeichnerDeklaration [ WSO '<=' WSO ( GanzzahlLiteral | GleitkommaLiteral | Zeichenkette ) ] .

Typ ::= 'int' | 'float' | 'string' .

BezeichnerDeklaration ::= Bezeichner [ '<' [ Zeichenkette ] ] [ '>' ] .

Bezeichner ::= ( Buchstabe | Grossbuchstabe ) { Buchstabe | Grossbuchstabe | Ziffer } .

Trennlinie ::= '...' { '.' } .

Ausfuehrungsteil ::= 'begin' WSO AnweisungsBlock 'end' .

AnweisungsBlock ::= { Anweisung } .

Anweisung ::= ( Zuweisung | BedingteAnweisung | Schleife | ForSchleife | Inkrementierung ) .

Zuweisung ::= Bezeichner WSO '<=' WSO ( GanzzahlLiteral | GleitkommaLiteral | Zeichenkette | Bezeichner | MathematischerAusdruck | StringAusdruck ) .

Inkrementierung ::= Bezeichner ( '++' | '--' ) .

MathematischerAusdruck ::= Summand { ('+' | '-') Summand } .
Summand ::= Faktor { ('*' | '/') Faktor } .
Faktor ::= Operand | Bezeichner | '(' WSO MathematischerAusdruck WSO ')' .
Operand ::= GanzzahlLiteral | GleitkommaLiteral .

StringAusdruck ::= StringAtom { '+' StringAtom } .
StringAtom ::= Zeichenkette | Bezeichner .
Zeichenkette ::= '"' { Buchstabe | Grossbuchstabe } '"' .

BoolescherAusdruck ::= Vergleich | '(' WSO BoolescherAusdruck WSO ')' | BoolescherAusdruck ( '&&' | '||' ) BoolescherAusdruck .
Vergleich ::= ( Bezeichner | GanzzahlLiteral | GleitkommaLiteral | Zeichenkette ) WSO VergleichsOperator WSO ( Bezeichner | GanzzahlLiteral | GleitkommaLiteral | Zeichenkette ) .
VergleichsOperator ::= '<' | '>' | '==' | '!=' .

BedingteAnweisung ::= 'if' '(' BoolescherAusdruck ')' ':' AnweisungsBlock [ 'else' ':' AnweisungsBlock ] 'end' .

Schleife ::= 'while' '(' BoolescherAusdruck ')' ':' AnweisungsBlock 'end' .

ForSchleife ::= 'for' '(' [ Deklaration ] ';' BoolescherAusdruck ';' [ Zuweisung ] ')' ':' AnweisungsBlock 'end' .

GanzzahlLiteral ::= Ziffer { Ziffer } .
GleitkommaLiteral ::= Ziffer { Ziffer } '.' Ziffer { Ziffer } .
Ziffer ::= '0' | '1' | '2' | '3' | '4' | '5' | '6' | '7' | '8' | '9' .

Buchstabe ::= 'a'...'z' .
Grossbuchstabe ::= 'A'...'Z' .

Leerzeichen ::= ' ' { ' ' } .
WSO ::= [ Leerzeichen ] .   -- optionales Leerzeichen

}

#### Semantik:

- Variablen müssen deklariert werden, bevor sie verwendet werden.
- Typen sind strikt: keine automatische Konvertierung zwischen Zahlen und Strings.
- `<` und `>` bei Deklarationen kennzeichnen Eingabe bzw. Ausgabe.
- In Schleifen und if/else-Blöcken dürfen mehrere Anweisungen stehen.
- Leerzeichen sind nur erlaubt, wo explizit durch `WSO` vorgesehen.
