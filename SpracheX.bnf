G = ( N, Z, T, P )

N = {
  Z, Programm, Deklarationsteil, Deklaration, Trennlinie, Ausfuehrungsteil, Anweisung, Zuweisung, Inkrementierung, BedingteAnweisung, Schleife, ForSchleife, Typ,
  BezeichnerDeklaration, Bezeichner, StringAusdruck, StringAtom, Charakterliste, MathematischerAusdruck, Summand, Faktor, Operand, Integer, Gleitkommazahl, Bedingung,
  VergleichsOperator, Zahl, Buchstabe, Grossbuchstabe, Leerzeichen
}


T = { ' ', 'A', 'B', 'C', 'D', 'E', 'F', 'G', 'H', 'I', 'J', 'K', 'L', 'M', 'N', 'O', 'P', 'Q', 'R', 'S', 'T', 'U', 'V', 'W', 'X', 'Y', 'Z', 
	'a', 'b', 'c', 'd', 'e', 'f', 'g', 'h', 'i', 'j', 'k', 'l', 'm', 'n', 'o', 'p', 'q', 'r', 's', 't', 'u', 'v', 'w', 'x', 'y', 'z', 
	'0', '1', '2', '3', '4', '5', '6', '7', '8', '9', ',', '.', '=>', '"', ':', '+', '-', '*', '/', '(', ')', '<', '>', '++', '--', '&&', '||',
	'int', 'float', 'string', 'if', 'else', 'while', 'begin', 'end', 'for', '...', 'program' }

P = {

Z ::= Programm Deklarationsteil Trennlinie Ausfuehrungsteil .
Deklarationsteil ::= {Deklaration} .

Zahl ::= '0' | '1' | '2' | '3' | '4' | '5' | '6' | '7' | '8' | '9' .
Buchstabe ::= 'a' | 'b' | 'c' | 'd' | 'e' | 'f' | 'g' | 'h' | 'i' | 'j' | 'k' | 'l' | 'm' | 'n' | 'o' | 'p' | 'q' | 'r' | 's' | 't' | 'u' | 'v' | 'w' | 'x' | 'y' | 'z' .
Grossbuchstabe ::= 'A' | 'B' | 'C' | 'D' | 'E' | 'F' | 'G' | 'H' | 'I' | 'J' | 'K' | 'L' | 'M' | 'N' | 'O' | 'P' | 'Q' | 'R' | 'S' | 'T' | 'U' | 'V' | 'W' | 'X' | 'Y' | 'Z' .
VergleichsOperator ::= '<' | '>' | '==' | '!=' .
Inkrementierung ::= '++' | '--' .
Typ ::= 'int' | 'float' | 'string' .

Trennlinie ::= '...' {'.'}.

Ausfuehrungsteil ::= 'begin' { Anweisung } 'end' .

StringAusdruck ::= StringAtom { '+' StringAtom } .
StringAtom ::= Charakterliste | Bezeichner .
Charakterliste ::= '"' { Buchstabe | Grossbuchstabe } '"' .

Integer ::= Zahl { Zahl } .
Gleitkommazahl ::= Zahl { Zahl } '.' Zahl { Zahl } .
Leerzeichen ::= ' ' | ' ' { Leerzeichen } .
Operand ::= Integer | Gleitkommazahl .

Bezeichner ::= ( Buchstabe | Grossbuchstabe ) {Buchstabe | Grossbuchstabe | Zahl } .
BezeichnerDeklaration ::= Bezeichner ['<'[Charakterliste]] ['>'] .

Anweisung ::= ( Zuweisung | BedingteAnweisung | Schleife | ForSchleife | Inkrementierung ) .

Deklaration ::=  Typ Leerzeichen BezeichnerDeklaration '=>' ( Integer | Gleitkommazahl | Charakterliste )
	       | Typ Leerzeichen BezeichnerDeklaration .

Zuweisung ::= Bezeichner '=>' ( Integer | Gleitkommazahl | Charakterliste | Bezeichner | MathematischerAusdruck | StringAusdruck ) .

MathematischerAusdruck ::= Summand { ('+' | '-') Summand } .
Summand ::= Faktor { ('*' | '/') Faktor } .
Faktor ::= Operand | Bezeichner | '(' MathematischerAusdruck ')' .

Bedingung ::= ( Bezeichner | Integer | Gleitkommazahl | Charakterliste ) VergleichsOperator ( Bezeichner | Integer | Gleitkommazahl | Charakterliste ) | Bedingung ( '&&' | '||') Bedingung | '(' Bedingung ')' . 

BedingteAnweisung ::= 'if' '(' Bedingung ')' ':' Anweisung [ 'else' ':' Anweisung ] 'end' .

Schleife ::= 'while' '(' Bedingung ')' ':' Anweisung 'end' .

ForSchleife ::= 'for' '(' [ Deklaration ] ';' Bedingung ';' [ Zuweisung ] ')' ':' Anweisung 'end' .

Programm ::= 'program' Leerzeichen Bezeichner ':' .

}


####
- Bei der Zuweisung einer Zahl zu einem String wird dieser implizit die Stringdarstellung der Zahl benutzt.
- Vorangestellte Nullen bei Zahlen werden ignoriert.
- Bei der Zuweisung von Werten zu einer Variable muss die Eingabe dem Variablentyp entsprechen.
- Bevor der Variable ein Wert zugewiesen wird, muss diese deklariert werden.
- Bei der Deklaration angegebene "<" und ">" geben an, ob die Variable entweder bei Definition eingelesen (<) und/oder am Ende des Scopes ausgegeben (>) wird.
- Hinter dem "<" bei der Deklaration kann optional ein string angegeben werden, welcher vor dem Einlesen des Wertes ausgegeben wirds (Frage nach dem Wert)
- Wird ein Bezeichner in enem Mathematischen Ausdruck verwendet, so darf dieser nicht vom Typ String sein
- Wenn nicht explizit definiert kÃ¶nnen beliebig viele Leerzeichen zwischen N Terminalen existieren